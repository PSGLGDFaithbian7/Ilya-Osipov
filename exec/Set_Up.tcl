#!/usr/bin/env tclsh

# Check required directories and files
if {![file exists ./work]}  {
    file mkdir ./work
}

if {![file exists ./setup]} {
    error "missing ./setup directory"
}

if {![file exists ./setup/library.lst]} {
    error "missing ./setup/library.lst"
}

set DATE [clock format [clock seconds] -format %Y%m%d_%H%M]

proc unique {BeforeUni} {
    array unset uniarr
    set AfterUni {}
    foreach e $BeforeUni {
        if {![info exists uniarr($e)]} {
            lappend AfterUni $e
            set uniarr($e) 1
        }
    }
    return $AfterUni
}

# Read input file
set fileToRead  [open ./setup/library.lst r]
set fileToWrite [open ./work/script.tcl w]

# Initialize variables
set search_path {}
set library_file {}
set link_file {}
set symbolLibraries {}
set syntheticLibraries {}
set top_module ""

while {[gets $fileToRead line] >= 0} {
    set line [string trim [lindex [split $line "#"] 0]]

    if {[regexp {^LibraryPath:\s*(.+)$} $line _ LibraryPath]} {
        set LibraryPath [string trim $LibraryPath]
        lappend search_path $LibraryPath
        continue
    }

    if {[regexp {^LibraryFile:\s*(.+)$} $line _ LibraryFile]} {
        set LibraryFile [string trim $LibraryFile]
        lappend library_file $LibraryFile
        continue
    }

    if {[regexp {^LinkLibraryFile:\s*(.+)$} $line _ LinkFile]  } {
        set LinkFile [string trim $LinkFile]
        lappend link_file $LinkFile
        continue
    }

    if {[regexp {^SymbolLibrary:\s*(.+)$} $line _ sdbRaw]} {
        set sdb [string trim $sdbRaw]
        lappend symbolLibraries $sdb
        continue
    }

    if {[regexp {^SyntheticLibrary:\s*(.+)$} $line _ sldbRaw]} {
        set sldb [string trim $sldbRaw]
        lappend syntheticLibraries $sldb
        continue
    }

    if {[regexp {^TopModule:\s*(.+)$} $line _ TopModule]} {
        set top_module [string trim $TopModule]
        continue
    }
}

close $fileToRead

# Check for required top_module
if {$top_module eq ""} {
    error "TopModule not specified in library.lst"
}

# Remove duplicates
set search_path        [unique $search_path]
set library_file       [unique $library_file]
set link_file          [unique $link_file]
set symbolLibraries    [unique $symbolLibraries]
set syntheticLibraries [unique $syntheticLibraries]

# Convert library_file entries to absolute paths under ../lib/
set scriptDir [file dirname [file normalize [info script]]]
set parentDir [file dirname $scriptDir]
set libDir [file join $parentDir lib]
set abs_library_file {}
foreach lf $library_file {
    set baseName [file tail $lf]
    set absPath [file normalize [file join $libDir $baseName]]
    lappend abs_library_file $absPath
}

# Write output script
puts $fileToWrite "# Auto-generated by Tcl Script"
puts $fileToWrite [format "# Generated at %s" [clock format [clock seconds] -format {%Y-%m-%d %H:%M:%S}]]
puts $fileToWrite ""
puts $fileToWrite "######### Set Library (for dc_shell -t) ###########"
puts $fileToWrite "remove_design -all"
puts $fileToWrite "set_host_options -max_cores 16"
puts $fileToWrite "set_svf  ../output/${top_module}_${DATE}.svf"

if {[llength $search_path] > 0} {
    puts $fileToWrite "set_app_var search_path \"[join $search_path \" \"]\""
}
if {[llength $abs_library_file] > 0} {
    puts $fileToWrite "set_app_var target_library \"[join $abs_library_file \" \"]\""
}
if {[llength $abs_library_file] > 0 || [llength $link_file] > 0} {
    puts $fileToWrite "set_app_var link_library \"* [join $abs_library_file \" \"] [join $link_file \" \"] [join $symbolLibraries \" \"] [join $syntheticLibraries \" \"]\""
}
if {[llength $symbolLibraries] > 0} {
    puts $fileToWrite "set_app_var symbol_library \"[join $symbolLibraries \" \"]\""
}
if {[llength $syntheticLibraries] > 0} {
    puts $fileToWrite "set_app_var synthetic_library \"[join $syntheticLibraries \" \"]\""
}

close $fileToWrite